<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="init()">
	<mx:Script>
		<![CDATA[
			import com.adobe.images.JPGEncoder;
			
			import flash.filesystem.File;
			import flash.filesystem.FileMode;
			import flash.filesystem.FileStream;
			import flash.utils.setTimeout;
			
			import mx.core.Window;
			import mx.events.FileEvent;
			import mx.messaging.channels.StreamingAMFChannel;
		
			private var file1:File = new File;
			private var file2:File = new File;
			private var bitmap:Bitmap;
			private var bitmapData:BitmapData;
			private function init():void{
				bitmap = new Bitmap();
				uiImage.addChild(bitmap);
			}
			private function cutHandler():void{
				if(Data.file.nativePath==null){
					output.text += "文件路径不能为空\n";
					return;
				}
				if(Data.bitmapData==null){
					output.text += "地图资源不存在或未加载完\n";
					return;
				}
				output.text = "";
				btn1.enabled = false;
				bitmapData = Data.bitmapData;
				var w:Number = Number(inWidth.text);
				var h:Number = Number(inHeight.text);
				if( w>0 && h>0){
					output.text += "开始切割\n";
					cutImage(bitmapData,w,h,0,0);
				}
				else{
					output.text += "请正确设置切割尺寸\n";
				}
			}
			private function cutImage(bitmapData:BitmapData,w:int,h:int,x:int,y:int):void{
				var bmd:BitmapData = new BitmapData(w, h);
				bmd.draw(bitmapData, new Matrix(1, 0, 0, 1, -x, -y), null, null, new Rectangle(0, 0, w, h));
				bitmap.bitmapData = bmd;
				var jpg:JPGEncoder = new JPGEncoder(80);
				var data:ByteArray = jpg.encode(bmd);
				var fileStream:FileStream = new FileStream;
				fileStream.open(new File(Data.file.nativePath+"\\"+(x/w)+"_"+(y/h)+".jpg"), FileMode.WRITE);
				fileStream.writeBytes(data);
				output.text += Data.file.nativePath+"\\"+(x/w)+"_"+(y/h)+".jpg 完成\n";
				output.verticalScrollPosition = output.maxVerticalScrollPosition;
				if(x+w < bitmapData.width){
					x += w;
				}
				else{
					x = 0;
					y += h;
				}
				if(y<bitmapData.height){
					setTimeout(cutImage,10,bitmapData,w,h,x,y);
				}
				else{
					output.text += "切割完成\n";
					btn1.enabled = true;
				}
			}
		]]>
	</mx:Script>
	<mx:Label x="40" y="15" text="切割图片宽:"/>
	<mx:Label x="150" y="15" text="切割图片高:"/>
	<mx:TextInput x="100" y="10" width="45" id="inWidth" text="200"/>
	<mx:TextInput x="210" y="10" width="45" id="inHeight" text="120"/>
	<mx:Button x="270" y="11" label="切割" click="cutHandler()" id="btn1"/>
	<mx:TextArea x="30" y="55" id="output" width="300" height="400"/>
	<mx:UIComponent id="uiImage" x="360" y="55" width="{int(inWidth.text)}" height="{int(inHeight.text)}" />
</mx:Canvas>
